plugins {
  id 'java'
  id 'application'
  id 'org.sonarqube' version '2.1'
  id 'com.jfrog.bintray' version '1.7.3'
  id "com.github.johnrengelman.shadow" version "1.2.4"
}

mainClassName = 'com.sedmelluq.discord.lavaplayer.Null'
ext.moduleName = 'lavaplayer'
version = '1.2.44'

dependencies {
  compile project(':common')

  compile "org.slf4j:slf4j-api:$slf4jVersion"

  compile 'org.apache.httpcomponents:httpclient:4.5.2'
  compile 'commons-io:commons-io:2.5'

  compile 'com.fasterxml.jackson.core:jackson-core:2.8.5'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.8.5'

  compile 'org.jsoup:jsoup:1.10.1'

  compile 'net.iharder:base64:2.3.9'
}

compileJava.dependsOn(':natives:checkNatives')

task updateVersion {
  new File("$projectDir/src/main/resources/com/sedmelluq/discord/lavaplayer/tools/version.txt").with {
    it.parentFile.mkdirs()
    it.text = version
  }
}

classes.finalizedBy(tasks['updateVersion'])

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId rootProject.group
      artifactId moduleName

      from components.java

      artifact sourceJar {
        classifier 'sources'
      }
    }
  }
}

apply from: "${rootDir}/gradle/bintray.gradle"
